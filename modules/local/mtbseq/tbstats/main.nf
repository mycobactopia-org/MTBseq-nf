process TBSTATS {
    tag "cohort"
    label 'process_single_high_memory'

    conda "${moduleDir}/environment.yml"

    container "${ workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container ?
        'https://depot.galaxyproject.org/singularity/mtbseq:1.1.0--hdfd78af_0' :
        'biocontainers/mtbseq:1.1.0--hdfd78af_0' }"



    input:
        path("Bam/*")
        path("Position_Tables/*")
        env(USER)
        tuple path(ref_resistance_list), path(ref_interesting_regions), path(ref_gene_categories), path(ref_base_quality_recalibration)

    output:
        path("Statistics/Mapping_and_Variant_Statistics.tab"), emit: statistics

    script:
        def args = task.ext.args ?: "--project mtbseqnf"

        """
        mkdir Statistics

        MTBseq --step TBstats \\
            --threads ${task.cpus} \\
            --resilist ${ref_resistance_list} \\
            --intregions ${ref_interesting_regions} \\
            --categories ${ref_gene_categories} \\
            --basecalib ${ref_base_quality_recalibration} \\
            ${args} \\
        1>>.command.out \\
        2>>.command.err \\
        || true               # NOTE This is a hack to overcome the exit status 1 thrown by mtbseq

        """

    stub:

        """
        sleep \$[ ( \$RANDOM % 10 )  + 1 ]s

        echo "MTBseq --step TBstats \
            --threads ${task.cpus} \
            --project mtbseqnf \
            --resilist ${ref_resistance_list} \
            --intregions ${ref_interesting_regions} \
            --categories ${ref_gene_categories} \
            --basecalib ${ref_base_quality_recalibration}"

        mkdir Statistics
        touch Statistics/Mapping_and_Variant_Statistics.tab
        """

}
